buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

apply plugin: 'com.android.application'

//target exported android project path
def tarProjDir = hasProperty('targetProjDir') ? targetProjDir + '/' : ''

println(String.format('targetProjDir:       %s', tarProjDir))
println(String.format('buildDir:            %s', project.buildDir))
println(String.format('archivesBaseName:    %s', archivesBaseName))
android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
    }
    
    lintOptions {
        disable 'HardcodedDebugMode'    //for unity generate manifest
    }
    
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    
	sourceSets {
        main {
            manifest.srcFile    tarProjDir + 'AndroidManifest.xml'
            java.srcDir         tarProjDir + 'src'
            resources.srcDir    tarProjDir + 'src'
            aidl.srcDir         tarProjDir + 'src'
            res.srcDir          tarProjDir + 'res'
            assets.srcDir       tarProjDir + 'assets'
            jniLibs.srcDir      tarProjDir + 'libs'
        }
	}
}

dependencies {
    compile         fileTree(dir: tarProjDir + 'libs', include: '*.jar')
}
